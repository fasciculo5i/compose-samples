/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId 'com.example.jetsnack'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.3'
        allWarningsAsErrors = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion '1.3.70-dev-withExperimentalGoogleExtensions-20200424'
        kotlinCompilerExtensionVersion compose_version
    }
}

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0-alpha01'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    def coilVersion = "0.1.6.ui-$snapshot-SNAPSHOT"
    implementation "dev.chrisbanes.accompanist:accompanist-coil:$coilVersion"
    implementation "androidx.ui:ui-core:$compose_version"
    implementation "androidx.ui:ui-layout:$compose_version"
    implementation "androidx.ui:ui-material:$compose_version"
    implementation "androidx.ui:ui-material-icons-extended:$compose_version"
    implementation "androidx.ui:ui-tooling:$compose_version"
    ktlint "com.pinterest:ktlint:0.37.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = 'Check Kotlin code style.'
    main = 'com.pinterest.ktlint.Main'
    classpath = configurations.ktlint
    args 'src/**/*.kt'
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: 'formatting') {
    description = 'Fix Kotlin code style deviations.'
    main = 'com.pinterest.ktlint.Main'
    classpath = configurations.ktlint
    args '-F', 'src/**/*.kt'
}
